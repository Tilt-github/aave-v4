// SPDX-License-Identifier: UNLICENSED
// Copyright (c) 2025 Aave Labs
pragma solidity ^0.8.10;

import 'tests/unit/Hub/HubBase.t.sol';

contract HubConfiguratorTest is HubBase {
  using SafeCast for uint256;

  HubConfigurator public hubConfigurator;

  address public HUB_CONFIGURATOR_ADMIN = makeAddr('HUB_CONFIGURATOR_ADMIN');
  uint256 public assetId;
  bytes public encodedIrData;

  address[4] public spokeAddresses;
  address spoke;

  function setUp() public virtual override {
    super.setUp();
    hubConfigurator = new HubConfigurator(HUB_CONFIGURATOR_ADMIN);
    IAccessManager accessManager = IAccessManager(hub1.authority());
    // Grant hubConfigurator hub admin role with 0 delay
    vm.prank(ADMIN);
    accessManager.grantRole(Roles.HUB_ADMIN_ROLE, address(hubConfigurator), 0);
    assetId = daiAssetId;
    encodedIrData = abi.encode(
      IAssetInterestRateStrategy.InterestRateData({
        optimalUsageRatio: 90_00, // 90.00%
        baseVariableBorrowRate: 5_00, // 5.00%
        variableRateSlope1: 5_00, // 5.00%
        variableRateSlope2: 5_00 // 5.00%
      })
    );
    spokeAddresses = [address(spoke1), address(spoke2), address(spoke3), address(treasurySpoke)];
    spoke = address(spoke1);
  }

  function test_addAsset_fuzz_revertsWith_OwnableUnauthorizedAccount(address caller) public {
    vm.assume(caller != HUB_CONFIGURATOR_ADMIN);

    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, caller));
    vm.prank(caller);
    _addAsset({
      fetchErc20Decimals: vm.randomBool(),
      underlying: vm.randomAddress(),
      decimals: bound(vm.randomUint(), 0, Constants.MAX_ALLOWED_ASSET_DECIMALS).toUint8(),
      feeReceiver: vm.randomAddress(),
      interestRateStrategy: vm.randomAddress(),
      encodedIrData: encodedIrData
    });
  }

  function test_addAsset_reverts_invalidIrData() public {
    vm.expectRevert();
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    _addAsset({
      fetchErc20Decimals: vm.randomBool(),
      underlying: vm.randomAddress(),
      decimals: 10,
      feeReceiver: vm.randomAddress(),
      interestRateStrategy: vm.randomAddress(),
      encodedIrData: abi.encode('invalid')
    });
  }

  function test_addAsset_fuzz_revertsWith_InvalidAssetDecimals(
    bool fetchErc20Decimals,
    address underlying,
    uint8 decimals,
    address feeReceiver,
    address interestRateStrategy
  ) public {
    assumeUnusedAddress(underlying);
    assumeNotZeroAddress(feeReceiver);
    assumeNotZeroAddress(interestRateStrategy);

    decimals = bound(decimals, Constants.MAX_ALLOWED_ASSET_DECIMALS + 1, type(uint8).max).toUint8();

    vm.expectRevert(IHub.InvalidAssetDecimals.selector, address(hub1));
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    _addAsset(
      fetchErc20Decimals,
      underlying,
      decimals,
      feeReceiver,
      interestRateStrategy,
      encodedIrData
    );
  }

  function test_addAsset_revertsWith_InvalidAddress_underlying() public {
    uint8 decimals = uint8(vm.randomUint(0, Constants.MAX_ALLOWED_ASSET_DECIMALS));
    address feeReceiver = makeAddr('newFeeReceiver');
    address interestRateStrategy = makeAddr('newIrStrategy');

    vm.expectRevert(IHub.InvalidAddress.selector, address(hub1));
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    _addAsset(true, address(0), decimals, feeReceiver, interestRateStrategy, encodedIrData);
  }

  function test_addAsset_revertsWith_InvalidAddress_irStrategy() public {
    address underlying = makeAddr('newUnderlying');
    uint8 decimals = uint8(vm.randomUint(0, Constants.MAX_ALLOWED_ASSET_DECIMALS));
    address feeReceiver = makeAddr('newFeeReceiver');

    vm.expectRevert(IHub.InvalidAddress.selector, address(hub1));
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    _addAsset(true, underlying, decimals, feeReceiver, address(0), encodedIrData);
  }

  function test_addAsset_fuzz(
    bool fetchErc20Decimals,
    address underlying,
    uint8 decimals,
    address feeReceiver,
    uint16 optimalUsageRatio,
    uint32 baseVariableBorrowRate,
    uint32 variableRateSlope1,
    uint32 variableRateSlope2
  ) public {
    assumeUnusedAddress(underlying);
    assumeNotZeroAddress(feeReceiver);

    decimals = bound(decimals, 0, Constants.MAX_ALLOWED_ASSET_DECIMALS).toUint8();
    optimalUsageRatio = bound(optimalUsageRatio, MIN_OPTIMAL_RATIO, MAX_OPTIMAL_RATIO).toUint16();

    baseVariableBorrowRate = bound(baseVariableBorrowRate, 0, MAX_BORROW_RATE / 3).toUint32();
    uint32 remainingAfterBase = MAX_BORROW_RATE.toUint32() - baseVariableBorrowRate;
    variableRateSlope1 = bound(variableRateSlope1, 0, remainingAfterBase / 2).toUint32();
    variableRateSlope2 = bound(
      variableRateSlope2,
      variableRateSlope1,
      MAX_BORROW_RATE - baseVariableBorrowRate - variableRateSlope1
    ).toUint32();

    uint256 expectedAssetId = hub1.getAssetCount();
    address interestRateStrategy = address(new AssetInterestRateStrategy(address(hub1)));

    encodedIrData = abi.encode(
      IAssetInterestRateStrategy.InterestRateData({
        optimalUsageRatio: optimalUsageRatio,
        baseVariableBorrowRate: baseVariableBorrowRate,
        variableRateSlope1: variableRateSlope1,
        variableRateSlope2: variableRateSlope2
      })
    );

    DataTypes.AssetConfig memory expectedConfig = DataTypes.AssetConfig({
      liquidityFee: 0,
      feeReceiver: feeReceiver,
      irStrategy: interestRateStrategy,
      reinvestmentController: address(0)
    });
    DataTypes.SpokeConfig memory expectedSpokeConfig = DataTypes.SpokeConfig({
      addCap: Constants.MAX_CAP,
      drawCap: Constants.MAX_CAP,
      active: true
    });

    vm.expectCall(
      address(hub1),
      abi.encodeCall(
        IHub.addAsset,
        (underlying, decimals, feeReceiver, interestRateStrategy, encodedIrData)
      )
    );

    vm.expectCall(
      address(hub1),
      abi.encodeCall(IHub.addSpoke, (expectedAssetId, feeReceiver, expectedSpokeConfig))
    );

    vm.prank(HUB_CONFIGURATOR_ADMIN);
    assetId = _addAsset(
      fetchErc20Decimals,
      underlying,
      decimals,
      feeReceiver,
      interestRateStrategy,
      encodedIrData
    );

    assertEq(assetId, expectedAssetId, 'asset id');
    assertEq(hub1.getAssetCount(), assetId + 1, 'asset count');
    assertEq(hub1.getAsset(assetId).decimals, decimals, 'asset decimals');
    assertEq(hub1.getAssetConfig(assetId), expectedConfig);
    assertEq(hub1.getSpokeConfig(assetId, feeReceiver), expectedSpokeConfig);
    assertEq(hub1.getAsset(assetId).reinvestmentController, address(0)); // should init to addr(0)
  }

  function test_updateLiquidityFee_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.updateLiquidityFee(address(hub1), vm.randomUint(), vm.randomUint());
  }

  function test_updateLiquidityFee_revertsWith_InvalidLiquidityFee() public {
    assetId = vm.randomUint(0, hub1.getAssetCount() - 1);
    uint16 liquidityFee = uint16(
      vm.randomUint(PercentageMath.PERCENTAGE_FACTOR + 1, type(uint16).max)
    );

    vm.expectRevert(IHub.InvalidLiquidityFee.selector);
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateLiquidityFee(address(hub1), assetId, liquidityFee);
  }

  function test_updateLiquidityFee_fuzz(uint256 assetId, uint16 liquidityFee) public {
    assetId = bound(assetId, 0, hub1.getAssetCount() - 1);
    liquidityFee = uint16(bound(liquidityFee, 0, PercentageMath.PERCENTAGE_FACTOR));

    DataTypes.AssetConfig memory expectedConfig = hub1.getAssetConfig(assetId);
    expectedConfig.liquidityFee = liquidityFee;

    vm.expectCall(address(hub1), abi.encodeCall(IHub.updateAssetConfig, (assetId, expectedConfig)));

    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateLiquidityFee(address(hub1), assetId, expectedConfig.liquidityFee);

    assertEq(hub1.getAssetConfig(assetId), expectedConfig);
  }

  function test_updateFeeReceiver_fuzz_revertsWith_OwnableUnauthorizedAccount(
    address caller
  ) public {
    vm.assume(caller != HUB_CONFIGURATOR_ADMIN);
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, caller));
    vm.prank(caller);
    hubConfigurator.updateFeeReceiver(address(hub1), vm.randomUint(), vm.randomAddress());
  }

  function test_updateFeeReceiver_revertsWith_InvalidAddress_spoke() public {
    assetId = vm.randomUint(0, hub1.getAssetCount() - 1);

    vm.expectRevert(IHub.InvalidAddress.selector, address(hub1));
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateFeeReceiver(address(hub1), assetId, address(0));
  }

  function test_updateFeeReceiver_fuzz(address feeReceiver) public {
    assumeNotZeroAddress(feeReceiver);

    DataTypes.AssetConfig memory oldConfig = hub1.getAssetConfig(assetId);

    if (feeReceiver != oldConfig.feeReceiver) {
      vm.expectCall(
        address(hub1),
        abi.encodeCall(
          IHub.updateSpokeConfig,
          (
            assetId,
            oldConfig.feeReceiver,
            DataTypes.SpokeConfig({
              addCap: 0,
              drawCap: 0,
              active: hub1.getSpokeConfig(assetId, oldConfig.feeReceiver).active
            })
          )
        )
      );

      if (!hub1.isSpokeListed(assetId, feeReceiver)) {
        vm.expectCall(
          address(hub1),
          abi.encodeCall(
            IHub.addSpoke,
            (
              assetId,
              feeReceiver,
              DataTypes.SpokeConfig({
                addCap: Constants.MAX_CAP,
                drawCap: Constants.MAX_CAP,
                active: true
              })
            )
          )
        );
      } else {
        vm.expectCall(
          address(hub1),
          abi.encodeCall(
            IHub.updateSpokeConfig,
            (
              assetId,
              feeReceiver,
              DataTypes.SpokeConfig({
                addCap: Constants.MAX_CAP,
                drawCap: Constants.MAX_CAP,
                active: hub1.getSpokeConfig(assetId, feeReceiver).active
              })
            )
          )
        );
      }

      // same struct, renaming to expectedConfig
      DataTypes.AssetConfig memory expectedConfig = oldConfig;
      expectedConfig.feeReceiver = feeReceiver;

      vm.expectCall(
        address(hub1),
        abi.encodeCall(IHub.updateAssetConfig, (assetId, expectedConfig))
      );

      vm.prank(HUB_CONFIGURATOR_ADMIN);
      hubConfigurator.updateFeeReceiver(address(hub1), assetId, feeReceiver);

      assertEq(hub1.getAssetConfig(assetId), expectedConfig);
    }
  }

  /// @dev Test update fee receiver and fees can still be withdrawn from old fee receiver
  function test_updateFeeReceiver_WithdrawFromOldSpoke() public {
    assertEq(
      hub1.getAssetConfig(daiAssetId).feeReceiver,
      address(treasurySpoke),
      'current fee receiver matches treasury spoke'
    );

    // Create debt to build up fees on the existing treasury spoke
    _addAndDrawLiquidity(
      hub1,
      daiAssetId,
      bob,
      address(spoke1),
      1000e18,
      bob,
      address(spoke1),
      100e18,
      365 days
    );

    assertGe(treasurySpoke.getSuppliedShares(daiAssetId), 0);
    uint256 fees = treasurySpoke.getSuppliedAmount(daiAssetId);

    // Change the fee receiver
    TreasurySpoke newTreasurySpoke = new TreasurySpoke(HUB_ADMIN, address(hub1));
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateFeeReceiver(address(hub1), daiAssetId, address(newTreasurySpoke));

    assertEq(
      hub1.getAssetConfig(daiAssetId).feeReceiver,
      address(newTreasurySpoke),
      'new fee receiver updated'
    );
    assertTrue(
      hub1.getSpokeConfig(daiAssetId, address(treasurySpoke)).active,
      'old fee receiver is not active'
    );

    // Withdraw fees from the old treasury spoke
    Utils.withdraw(
      ISpoke(address(treasurySpoke)),
      daiAssetId,
      TREASURY_ADMIN,
      fees,
      address(treasurySpoke)
    );

    assertEq(treasurySpoke.getSuppliedAmount(daiAssetId), 0, 'old treasury spoke should be empty');

    // Accrue more fees, this time to new fee receiver
    skip(365 days);

    assertGt(
      newTreasurySpoke.getSuppliedAmount(daiAssetId),
      0,
      'new fee receiver should have accrued fees'
    );
    assertEq(treasurySpoke.getSuppliedAmount(daiAssetId), 0, 'old fee receiver should be empty');
  }

  /// @dev Test update fee receiver and old fee receiver still accrues fees
  function test_updateFeeReceiver_correctAccruals() public {
    // Ensure current fee receiver is the treasury spoke
    assertEq(
      hub1.getAssetConfig(daiAssetId).feeReceiver,
      address(treasurySpoke),
      'old fee receiver mismatch'
    );

    // Create debt to build up fees on the existing treasury spoke
    _addAndDrawLiquidity(
      hub1,
      daiAssetId,
      bob,
      address(spoke1),
      1000e18,
      bob,
      address(spoke1),
      100e18,
      365 days
    );

    assertGe(treasurySpoke.getSuppliedShares(daiAssetId), 0);
    uint256 feeShares = treasurySpoke.getSuppliedShares(daiAssetId);

    // Change the fee receiver
    TreasurySpoke newTreasurySpoke = new TreasurySpoke(HUB_ADMIN, address(hub1));
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateFeeReceiver(address(hub1), daiAssetId, address(newTreasurySpoke));

    // Ensure fee receiver was updated
    assertEq(
      hub1.getAssetConfig(daiAssetId).feeReceiver,
      address(newTreasurySpoke),
      'new fee receiver mismatch'
    );

    // Ensure old fee receiver is still active
    assertTrue(
      hub1.getSpokeConfig(daiAssetId, address(treasurySpoke)).active,
      'old fee receiver is not active'
    );

    // Withdraw half the fee shares from the old treasury spoke
    Utils.withdraw(
      ISpoke(address(treasurySpoke)),
      daiAssetId,
      TREASURY_ADMIN,
      hub1.convertToAddedAssets(daiAssetId, feeShares / 2),
      address(treasurySpoke)
    );

    // Get the remaining fee shares
    feeShares = treasurySpoke.getSuppliedShares(daiAssetId);

    // Accrue more fees, this time to new fee receiver
    skip(365 days);

    // Check that new fee receiver is getting the fees, and not old treasury spoke
    assertGt(
      newTreasurySpoke.getSuppliedAmount(daiAssetId),
      0,
      'new fee receiver should have accrued fees'
    );
    assertEq(
      treasurySpoke.getSuppliedShares(daiAssetId),
      feeShares,
      'old fee receiver should still have same share amount'
    );

    // Now withdraw remaining fee shares from old treasury spoke
    Utils.withdraw(
      ISpoke(address(treasurySpoke)),
      daiAssetId,
      TREASURY_ADMIN,
      UINT256_MAX,
      address(treasurySpoke)
    );
    assertEq(treasurySpoke.getSuppliedShares(daiAssetId), 0, 'old fee receiver should be empty');
  }

  function test_updateFeeReceiver_Scenario() public {
    // set same fee receiver
    test_updateFeeReceiver_fuzz(address(treasurySpoke));
    // set new fee receiver
    test_updateFeeReceiver_fuzz(makeAddr('newFeeReceiver'));
    // set initial fee receiver
    test_updateFeeReceiver_fuzz(address(treasurySpoke));
  }

  function test_updateFeeConfig_fuzz_revertsWith_OwnableUnauthorizedAccount(address caller) public {
    vm.assume(caller != HUB_CONFIGURATOR_ADMIN);
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, caller));
    vm.prank(caller);
    hubConfigurator.updateFeeConfig({
      hub: address(hub1),
      assetId: vm.randomUint(),
      liquidityFee: vm.randomUint(),
      feeReceiver: vm.randomAddress()
    });
  }

  function test_updateFeeConfig_revertsWith_InvalidAddress_spoke() public {
    uint256 assetId = vm.randomUint(0, hub1.getAssetCount() - 1);
    uint256 liquidityFee = vm.randomUint(1, PercentageMath.PERCENTAGE_FACTOR);

    vm.expectRevert(IHub.InvalidAddress.selector, address(hub1));
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateFeeConfig(address(hub1), assetId, liquidityFee, address(0));
  }

  function test_updateFeeConfig_revertsWith_InvalidLiquidityFee() public {
    uint256 assetId = vm.randomUint(0, hub1.getAssetCount() - 1);
    uint16 liquidityFee = uint16(
      vm.randomUint(PercentageMath.PERCENTAGE_FACTOR + 1, type(uint16).max)
    );
    address feeReceiver = hub1.getAssetConfig(assetId).feeReceiver;

    vm.expectRevert(IHub.InvalidLiquidityFee.selector);
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateFeeConfig(address(hub1), assetId, liquidityFee, feeReceiver);
  }

  function test_updateFeeConfig_fuzz(
    uint256 assetId,
    uint16 liquidityFee,
    address feeReceiver
  ) public {
    assetId = bound(assetId, 0, hub1.getAssetCount() - 1);
    liquidityFee = uint16(bound(liquidityFee, 0, PercentageMath.PERCENTAGE_FACTOR));
    assumeNotZeroAddress(feeReceiver);

    DataTypes.AssetConfig memory oldConfig = hub1.getAssetConfig(assetId);

    if (oldConfig.feeReceiver != feeReceiver) {
      vm.expectCall(
        address(hub1),
        abi.encodeCall(
          IHub.updateSpokeConfig,
          (
            assetId,
            oldConfig.feeReceiver,
            DataTypes.SpokeConfig({
              addCap: 0,
              drawCap: 0,
              active: hub1.getSpokeConfig(assetId, oldConfig.feeReceiver).active
            })
          )
        )
      );

      if (!hub1.isSpokeListed(assetId, feeReceiver)) {
        vm.expectCall(
          address(hub1),
          abi.encodeCall(
            IHub.addSpoke,
            (
              assetId,
              feeReceiver,
              DataTypes.SpokeConfig({
                addCap: Constants.MAX_CAP,
                drawCap: Constants.MAX_CAP,
                active: true
              })
            )
          )
        );
      } else {
        vm.expectCall(
          address(hub1),
          abi.encodeCall(
            IHub.updateSpokeConfig,
            (
              assetId,
              feeReceiver,
              DataTypes.SpokeConfig({
                addCap: Constants.MAX_CAP,
                drawCap: Constants.MAX_CAP,
                active: hub1.getSpokeConfig(assetId, feeReceiver).active
              })
            )
          )
        );
      }
    }

    // same struct, renaming to expectedConfig
    DataTypes.AssetConfig memory expectedConfig = oldConfig;
    expectedConfig.feeReceiver = feeReceiver;
    expectedConfig.liquidityFee = liquidityFee;

    vm.expectCall(address(hub1), abi.encodeCall(IHub.updateAssetConfig, (assetId, expectedConfig)));

    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateFeeConfig(address(hub1), assetId, liquidityFee, feeReceiver);

    assertEq(hub1.getAssetConfig(assetId), expectedConfig);
  }

  function test_updateFeeConfig_Scenario() public {
    // set same fee receiver and change liquidity fee
    test_updateFeeConfig_fuzz(0, 18_00, address(treasurySpoke));
    // set new fee receiver and liquidity fee
    test_updateFeeConfig_fuzz(0, 4_00, makeAddr('newFeeReceiver'));
    // set non-zero fee receiver
    test_updateFeeConfig_fuzz(0, 0, makeAddr('newFeeReceiver2'));
    // set initial fee receiver and zero fee
    test_updateFeeConfig_fuzz(0, 0, address(treasurySpoke));
  }

  function test_updateInterestRateStrategy_fuzz_revertsWith_OwnableUnauthorizedAccount(
    address caller
  ) public {
    vm.assume(caller != HUB_CONFIGURATOR_ADMIN);
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, caller));
    vm.prank(caller);
    hubConfigurator.updateInterestRateStrategy(address(hub1), vm.randomUint(), vm.randomAddress());
  }

  function test_updateInterestRateStrategy() public {
    address interestRateStrategy = makeAddr('newInterestRateStrategy');

    DataTypes.AssetConfig memory expectedConfig = hub1.getAssetConfig(assetId);
    expectedConfig.irStrategy = interestRateStrategy;
    _mockInterestRateBps(interestRateStrategy, 5_00);

    vm.expectCall(address(hub1), abi.encodeCall(IHub.updateAssetConfig, (assetId, expectedConfig)));

    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateInterestRateStrategy(address(hub1), assetId, interestRateStrategy);

    assertEq(hub1.getAssetConfig(assetId), expectedConfig);
  }

  function test_updateReinvestmentController_fuzz_revertsWith_OwnableUnauthorizedAccount(
    address caller
  ) public {
    vm.assume(caller != HUB_CONFIGURATOR_ADMIN);
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, caller));
    vm.prank(caller);
    hubConfigurator.updateReinvestmentController(
      address(hub1),
      vm.randomUint(),
      vm.randomAddress()
    );
  }

  function test_updateReinvestmentController() public {
    address reinvestmentController = makeAddr('newReinvestmentController');
    DataTypes.AssetConfig memory expectedConfig = hub1.getAssetConfig(assetId);
    expectedConfig.reinvestmentController = reinvestmentController;
    vm.expectCall(address(hub1), abi.encodeCall(IHub.updateAssetConfig, (assetId, expectedConfig)));
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateReinvestmentController(address(hub1), assetId, reinvestmentController);

    assertEq(hub1.getAssetConfig(assetId), expectedConfig);
  }

  function test_updateInterestRateStrategy_revertsWith_InvalidAddress_irStrategy() public {
    assetId = vm.randomUint(0, hub1.getAssetCount() - 1);

    vm.expectRevert(IHub.InvalidAddress.selector, address(hub1));
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateInterestRateStrategy(address(hub1), assetId, address(0));
  }

  function test_updateInterestRateStrategy_revertsWith_InterestRateStrategyReverts() public {
    assetId = vm.randomUint(0, hub1.getAssetCount() - 1);
    address interestRateStrategy = makeAddr('newInterestRateStrategy');

    vm.expectRevert();
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateInterestRateStrategy(address(hub1), assetId, interestRateStrategy);
  }

  function test_updateAssetConfig_fuzz_revertsWith_OwnableUnauthorizedAccount(
    address caller
  ) public {
    vm.assume(caller != HUB_CONFIGURATOR_ADMIN);
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, caller));
    vm.prank(caller);
    hubConfigurator.updateAssetConfig(
      address(hub1),
      vm.randomUint(),
      DataTypes.AssetConfig({
        liquidityFee: 0,
        feeReceiver: vm.randomAddress(),
        irStrategy: vm.randomAddress(),
        reinvestmentController: address(0)
      })
    );
  }

  function test_updateAssetConfig() public {
    DataTypes.AssetConfig memory newAssetConfig = DataTypes.AssetConfig({
      liquidityFee: 0,
      feeReceiver: makeAddr('newFeeReceiver'),
      irStrategy: makeAddr('newInterestRateStrategy'),
      reinvestmentController: address(0)
    });
    _mockInterestRateBps(newAssetConfig.irStrategy, 5_00);

    DataTypes.AssetConfig memory oldConfig = hub1.getAssetConfig(assetId);

    vm.expectCall(
      address(hub1),
      abi.encodeCall(
        IHub.updateSpokeConfig,
        (
          assetId,
          oldConfig.feeReceiver,
          DataTypes.SpokeConfig({addCap: 0, drawCap: 0, active: true})
        )
      )
    );
    vm.expectCall(
      address(hub1),
      abi.encodeCall(
        IHub.addSpoke,
        (
          assetId,
          newAssetConfig.feeReceiver,
          DataTypes.SpokeConfig({
            addCap: Constants.MAX_CAP,
            drawCap: Constants.MAX_CAP,
            active: true
          })
        )
      )
    );
    vm.expectCall(address(hub1), abi.encodeCall(IHub.updateAssetConfig, (assetId, newAssetConfig)));

    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateAssetConfig(address(hub1), assetId, newAssetConfig);

    assertEq(hub1.getAssetConfig(assetId), newAssetConfig);
  }

  function test_freezeAsset_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.freezeAsset(address(hub1), assetId);
  }

  function test_freezeAsset() public {
    for (uint256 i; i < spokeAddresses.length; i++) {
      DataTypes.SpokeConfig memory spokeConfig = hub1.getSpokeConfig(assetId, spokeAddresses[i]);
      spokeConfig.addCap = 0;
      spokeConfig.drawCap = 0;
      vm.expectCall(
        address(hub1),
        abi.encodeCall(IHub.updateSpokeConfig, (assetId, spokeAddresses[i], spokeConfig))
      );
    }

    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.freezeAsset(address(hub1), assetId);

    for (uint256 i; i < spokeAddresses.length; i++) {
      DataTypes.SpokeConfig memory spokeConfig = hub1.getSpokeConfig(assetId, spokeAddresses[i]);
      assertEq(spokeConfig.addCap, 0);
      assertEq(spokeConfig.drawCap, 0);
    }
  }

  function test_pauseAsset_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.pauseAsset(address(hub1), assetId);
  }

  function test_pauseAsset() public {
    for (uint256 i; i < spokeAddresses.length; i++) {
      DataTypes.SpokeConfig memory spokeConfig = hub1.getSpokeConfig(assetId, spokeAddresses[i]);
      spokeConfig.active = false;
      vm.expectCall(
        address(hub1),
        abi.encodeCall(IHub.updateSpokeConfig, (assetId, spokeAddresses[i], spokeConfig))
      );
    }

    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.pauseAsset(address(hub1), assetId);

    for (uint256 i; i < spokeAddresses.length; i++) {
      DataTypes.SpokeConfig memory spokeConfig = hub1.getSpokeConfig(assetId, spokeAddresses[i]);
      assertEq(spokeConfig.active, false);
    }
  }

  function test_addSpoke_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    DataTypes.SpokeConfig memory spokeConfig;
    hubConfigurator.addSpoke(address(hub1), vm.randomAddress(), 0, spokeConfig);
  }

  function test_addSpoke() public {
    address newSpoke = makeAddr('newSpoke');

    DataTypes.SpokeConfig memory daiSpokeConfig = DataTypes.SpokeConfig({
      addCap: 1,
      drawCap: 2,
      active: true
    });

    vm.expectEmit(address(hub1));
    emit IHub.AddSpoke(daiAssetId, newSpoke);
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.addSpoke(address(hub1), newSpoke, daiAssetId, daiSpokeConfig);

    assertEq(hub1.getSpokeConfig(daiAssetId, newSpoke), daiSpokeConfig);
  }

  function test_addSpokeToAssets_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.addSpokeToAssets(
      address(hub1),
      vm.randomAddress(),
      new uint256[](0),
      new DataTypes.SpokeConfig[](0)
    );
  }

  function test_addSpokeToAssets_revertsWith_MismatchedConfigs() public {
    uint256[] memory assetIds = new uint256[](2);
    assetIds[0] = daiAssetId;
    assetIds[1] = wethAssetId;

    DataTypes.SpokeConfig[] memory spokeConfigs = new DataTypes.SpokeConfig[](3);
    spokeConfigs[0] = DataTypes.SpokeConfig({addCap: 1, drawCap: 2, active: true});
    spokeConfigs[1] = DataTypes.SpokeConfig({addCap: 3, drawCap: 4, active: true});
    spokeConfigs[2] = DataTypes.SpokeConfig({addCap: 5, drawCap: 6, active: true});

    vm.expectRevert(IHubConfigurator.MismatchedConfigs.selector);
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.addSpokeToAssets(address(hub1), spoke, assetIds, spokeConfigs);
  }

  function test_addSpokeToAssets() public {
    address newSpoke = makeAddr('newSpoke');

    uint256[] memory assetIds = new uint256[](2);
    assetIds[0] = daiAssetId;
    assetIds[1] = wethAssetId;

    DataTypes.SpokeConfig memory daiSpokeConfig = DataTypes.SpokeConfig({
      addCap: 1,
      drawCap: 2,
      active: true
    });
    DataTypes.SpokeConfig memory wethSpokeConfig = DataTypes.SpokeConfig({
      addCap: 3,
      drawCap: 4,
      active: true
    });

    DataTypes.SpokeConfig[] memory spokeConfigs = new DataTypes.SpokeConfig[](2);
    spokeConfigs[0] = daiSpokeConfig;
    spokeConfigs[1] = wethSpokeConfig;

    vm.expectEmit(address(hub1));
    emit IHub.AddSpoke(daiAssetId, newSpoke);
    vm.expectEmit(address(hub1));
    emit IHub.AddSpoke(wethAssetId, newSpoke);
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.addSpokeToAssets(address(hub1), newSpoke, assetIds, spokeConfigs);

    DataTypes.SpokeConfig memory daiSpokeData = hub1.getSpokeConfig(daiAssetId, newSpoke);
    DataTypes.SpokeConfig memory wethSpokeData = hub1.getSpokeConfig(wethAssetId, newSpoke);

    assertEq(daiSpokeData, daiSpokeConfig);
    assertEq(wethSpokeData, wethSpokeConfig);
  }

  function test_updateSpokeActive_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.updateSpokeActive(address(hub1), assetId, spokeAddresses[0], true);
  }

  function test_updateSpokeActive() public {
    DataTypes.SpokeConfig memory expectedSpokeConfig = hub1.getSpokeConfig(assetId, spoke);
    for (uint256 i = 0; i < 2; ++i) {
      bool active = (i == 0) ? false : true;
      expectedSpokeConfig.active = active;
      vm.expectCall(
        address(hub1),
        abi.encodeCall(IHub.updateSpokeConfig, (assetId, spoke, expectedSpokeConfig))
      );
      vm.prank(HUB_CONFIGURATOR_ADMIN);
      hubConfigurator.updateSpokeActive(address(hub1), assetId, spoke, active);
      assertEq(hub1.getSpokeConfig(assetId, spoke), expectedSpokeConfig);
    }
  }

  function test_updateSpokeSupplyCap_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.updateSpokeSupplyCap(address(hub1), assetId, spokeAddresses[0], 100);
  }

  function test_updateSpokeSupplyCap() public {
    uint56 newSupplyCap = 100;
    DataTypes.SpokeConfig memory expectedSpokeConfig = hub1.getSpokeConfig(assetId, spoke);
    expectedSpokeConfig.addCap = newSupplyCap;
    vm.expectCall(
      address(hub1),
      abi.encodeCall(IHub.updateSpokeConfig, (assetId, spoke, expectedSpokeConfig))
    );
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateSpokeSupplyCap(address(hub1), assetId, spoke, newSupplyCap);
    assertEq(hub1.getSpokeConfig(assetId, spoke), expectedSpokeConfig);
  }

  function test_updateSpokeDrawCap_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.updateSpokeDrawCap(address(hub1), assetId, spokeAddresses[0], 100);
  }

  function test_updateSpokeDrawCap() public {
    uint56 newDrawCap = 100;
    DataTypes.SpokeConfig memory expectedSpokeConfig = hub1.getSpokeConfig(assetId, spoke);
    expectedSpokeConfig.drawCap = newDrawCap;
    vm.expectCall(
      address(hub1),
      abi.encodeCall(IHub.updateSpokeConfig, (assetId, spoke, expectedSpokeConfig))
    );
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateSpokeDrawCap(address(hub1), assetId, spoke, newDrawCap);
    assertEq(hub1.getSpokeConfig(assetId, spoke), expectedSpokeConfig);
  }

  function test_updateSpokeCaps_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.updateSpokeCaps(address(hub1), assetId, spokeAddresses[0], 100, 100);
  }

  function test_updateSpokeCaps() public {
    uint56 newSupplyCap = 100;
    uint56 newDrawCap = 200;
    DataTypes.SpokeConfig memory expectedSpokeConfig = hub1.getSpokeConfig(assetId, spoke);
    expectedSpokeConfig.addCap = newSupplyCap;
    expectedSpokeConfig.drawCap = newDrawCap;
    vm.expectCall(
      address(hub1),
      abi.encodeCall(IHub.updateSpokeConfig, (assetId, spoke, expectedSpokeConfig))
    );
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateSpokeCaps(address(hub1), assetId, spoke, newSupplyCap, newDrawCap);
    assertEq(hub1.getSpokeConfig(assetId, spoke), expectedSpokeConfig);
  }

  function test_updateSpokeConfig_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.updateSpokeConfig(
      address(hub1),
      assetId,
      spokeAddresses[0],
      DataTypes.SpokeConfig({addCap: 100, drawCap: 100, active: true})
    );
  }

  function test_updateSpokeConfig() public {
    DataTypes.SpokeConfig memory newSpokeConfig = DataTypes.SpokeConfig({
      addCap: 100,
      drawCap: 200,
      active: false
    });
    vm.expectCall(
      address(hub1),
      abi.encodeCall(IHub.updateSpokeConfig, (assetId, spoke, newSpokeConfig))
    );
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateSpokeConfig(address(hub1), assetId, spoke, newSpokeConfig);
    assertEq(hub1.getSpokeConfig(assetId, spoke), newSpokeConfig);
  }

  function test_pauseSpoke_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.pauseSpoke(address(hub1), address(spoke3));
  }

  function test_pauseSpoke() public {
    /// @dev Spoke3 is listed on hub1 on 4 assets: dai, weth, wbtc, usdx
    assertGt(hub1.getAssetCount(), 4, 'hub1 has less than 4 assets listed');

    for (uint256 assetId = 0; assetId < 4; ++assetId) {
      vm.expectCall(address(hub1), abi.encodeCall(IHub.isSpokeListed, (assetId, address(spoke3))));

      DataTypes.SpokeConfig memory expectedSpokeConfig = hub1.getSpokeConfig(
        assetId,
        address(spoke3)
      );
      expectedSpokeConfig.active = false;
      vm.expectCall(
        address(hub1),
        abi.encodeCall(IHub.updateSpokeConfig, (assetId, address(spoke3), expectedSpokeConfig))
      );
    }

    for (uint256 assetId = 4; assetId < hub1.getAssetCount(); ++assetId) {
      vm.expectCall(address(hub1), abi.encodeCall(IHub.isSpokeListed, (assetId, address(spoke3))));
    }

    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.pauseSpoke(address(hub1), address(spoke3));

    for (uint256 assetId = 0; assetId < 4; ++assetId) {
      DataTypes.SpokeConfig memory spokeConfig = hub1.getSpokeConfig(assetId, address(spoke3));
      assertEq(spokeConfig.active, false);
    }
  }

  function test_freezeSpoke_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.freezeSpoke(address(hub1), address(spoke3));
  }

  function test_freezeSpoke() public {
    /// @dev Spoke3 is listed on hub1 on 4 assets: dai, weth, wbtc, usdx
    assertGt(hub1.getAssetCount(), 4, 'hub1 has less than 4 assets listed');

    for (uint256 assetId = 0; assetId < 4; ++assetId) {
      vm.expectCall(address(hub1), abi.encodeCall(IHub.isSpokeListed, (assetId, address(spoke3))));

      DataTypes.SpokeConfig memory expectedSpokeConfig = hub1.getSpokeConfig(
        assetId,
        address(spoke3)
      );
      expectedSpokeConfig.addCap = 0;
      expectedSpokeConfig.drawCap = 0;
      vm.expectCall(
        address(hub1),
        abi.encodeCall(IHub.updateSpokeConfig, (assetId, address(spoke3), expectedSpokeConfig))
      );
    }

    for (uint256 assetId = 4; assetId < hub1.getAssetCount(); ++assetId) {
      vm.expectCall(address(hub1), abi.encodeCall(IHub.isSpokeListed, (assetId, address(spoke3))));
    }

    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.freezeSpoke(address(hub1), address(spoke3));

    for (uint256 assetId = 0; assetId < 4; ++assetId) {
      DataTypes.SpokeConfig memory spokeConfig = hub1.getSpokeConfig(assetId, address(spoke3));
      assertEq(spokeConfig.addCap, 0);
      assertEq(spokeConfig.drawCap, 0);
    }
  }

  function test_updateInterestRateData_revertsWith_OwnableUnauthorizedAccount() public {
    vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, alice));
    vm.prank(alice);
    hubConfigurator.updateInterestRateData(address(hub1), assetId, vm.randomBytes(32));
  }

  function test_updateInterestRateData() public {
    IAssetInterestRateStrategy.InterestRateData memory newIrData = IAssetInterestRateStrategy
      .InterestRateData({
        optimalUsageRatio: 90_00, // 90.00%
        baseVariableBorrowRate: 5_00, // 5.00%
        variableRateSlope1: 5_00, // 5.00%
        variableRateSlope2: 5_00 // 5.00%
      });

    vm.expectCall(
      address(hub1),
      abi.encodeCall(IHub.setInterestRateData, (assetId, abi.encode(newIrData)))
    );
    vm.prank(HUB_CONFIGURATOR_ADMIN);
    hubConfigurator.updateInterestRateData(address(hub1), assetId, abi.encode(newIrData));

    assertEq(irStrategy.getInterestRateData(assetId), newIrData);
  }

  function _addAsset(
    bool fetchErc20Decimals,
    address underlying,
    uint8 decimals,
    address feeReceiver,
    address interestRateStrategy,
    bytes memory encodedIrData
  ) internal returns (uint256) {
    if (fetchErc20Decimals) {
      _mockDecimals(underlying, decimals);
      return
        hubConfigurator.addAsset(
          address(hub1),
          underlying,
          feeReceiver,
          interestRateStrategy,
          encodedIrData
        );
    } else {
      return
        hubConfigurator.addAsset(
          address(hub1),
          underlying,
          decimals,
          feeReceiver,
          interestRateStrategy,
          encodedIrData
        );
    }
  }
}
