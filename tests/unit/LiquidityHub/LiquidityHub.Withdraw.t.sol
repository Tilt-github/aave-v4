// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import './LiquidityHubBase.t.sol';

contract LiquidityHubWithdrawTest is LiquidityHubBase {
  using SharesMath for uint256;
  using WadRayMath for uint256;

  function test_withdraw() public {
    uint256 amount = 100e18;

    // User supply
    Utils.supply({
      hub: hub,
      assetId: daiAssetId,
      spoke: address(spoke1),
      amount: amount,
      riskPremium: 0,
      user: alice,
      to: address(spoke1)
    });

    DataTypes.Asset memory assetData = hub.getAsset(daiAssetId);
    DataTypes.SpokeData memory spokeData = hub.getSpoke(daiAssetId, address(spoke1));

    uint256 timestamp = vm.getBlockTimestamp();

    // hub
    assertEq(hub.getTotalAssets(daiAssetId), amount, 'hub total assets pre-withdraw');
    // asset
    assertEq(
      assetData.suppliedShares,
      hub.convertToShares(daiAssetId, amount),
      'asset total shares pre-withdraw'
    );
    assertEq(assetData.availableLiquidity, amount, 'asset availableLiquidity pre-withdraw');
    assertEq(assetData.baseDebt, 0, 'asset baseDebt pre-withdraw');
    assertEq(assetData.outstandingPremium, 0, 'asset outstandingPremium pre-withdraw');
    assertEq(assetData.baseBorrowIndex, WadRayMath.RAY, 'asset baseBorrowIndex pre-withdraw');
    assertEq(
      assetData.baseBorrowRate,
      uint256(5_00).bpsToRay(),
      'asset baseBorrowRate pre-withdraw'
    );
    assertEq(assetData.riskPremium, 0, 'asset riskPremium pre-withdraw');
    assertEq(assetData.lastUpdateTimestamp, timestamp, 'asset lastUpdateTimestamp pre-withdraw');
    // spoke
    assertEq(
      spokeData.suppliedShares,
      assetData.suppliedShares,
      'spoke suppliedShares pre-withdraw'
    );
    assertEq(spokeData.baseDebt, assetData.baseDebt, 'spoke baseDebt pre-withdraw');
    assertEq(
      spokeData.outstandingPremium,
      assetData.outstandingPremium,
      'spoke outstandingPremium pre-withdraw'
    );
    assertEq(
      spokeData.baseBorrowIndex,
      assetData.baseBorrowIndex,
      'spoke baseBorrowIndex pre-withdraw'
    );
    assertEq(spokeData.riskPremium, 0, 'spoke riskPremium pre-withdraw');
    assertEq(
      spokeData.lastUpdateTimestamp,
      assetData.lastUpdateTimestamp,
      'spoke lastUpdateTimestamp pre-withdraw'
    );
    // dai
    assertEq(tokenList.dai.balanceOf(address(spoke1)), 0, 'spoke token balance pre-withdraw');
    assertEq(tokenList.dai.balanceOf(address(hub)), amount, 'hub token balance pre-withdraw');
    assertEq(
      tokenList.dai.balanceOf(alice),
      MAX_SUPPLY_AMOUNT - amount,
      'user token balance pre-withdraw'
    );

    vm.expectEmit(address(tokenList.dai));
    emit IERC20.Transfer(address(hub), alice, amount);
    vm.expectEmit(address(hub));
    emit ILiquidityHub.Withdraw(daiAssetId, address(spoke1), alice, amount);

    vm.prank(address(spoke1));
    hub.withdraw({assetId: daiAssetId, amount: amount, riskPremium: 0, to: alice});

    assetData = hub.getAsset(daiAssetId);
    spokeData = hub.getSpoke(daiAssetId, address(spoke1));

    // hub
    assertEq(hub.getTotalAssets(daiAssetId), 0, 'hub total assets post-withdraw');
    // asset
    assertEq(assetData.suppliedShares, 0, 'asset total shares post-withdraw');
    assertEq(assetData.availableLiquidity, 0, 'asset availableLiquidity post-withdraw');
    assertEq(assetData.baseDebt, 0, 'asset baseDebt post-withdraw');
    assertEq(assetData.outstandingPremium, 0, 'asset outstandingPremium post-withdraw');
    assertEq(assetData.baseBorrowIndex, WadRayMath.RAY, 'asset baseBorrowIndex post-withdraw');
    assertEq(
      assetData.baseBorrowRate,
      uint256(5_00).bpsToRay(),
      'asset baseBorrowRate post-withdraw'
    );
    assertEq(assetData.riskPremium, 0, 'asset riskPremium post-withdraw');
    assertEq(assetData.lastUpdateTimestamp, timestamp, 'asset lastUpdateTimestamp post-withdraw');
    // spoke
    assertEq(
      spokeData.suppliedShares,
      assetData.suppliedShares,
      'spoke suppliedShares post-withdraw'
    );
    assertEq(spokeData.baseDebt, assetData.baseDebt, 'spoke baseDebt post-withdraw');
    assertEq(
      spokeData.outstandingPremium,
      assetData.outstandingPremium,
      'spoke outstandingPremium post-withdraw'
    );
    assertEq(
      spokeData.baseBorrowIndex,
      assetData.baseBorrowIndex,
      'spoke baseBorrowIndex post-withdraw'
    );
    assertEq(spokeData.riskPremium, 0, 'spoke riskPremium post-withdraw');
    assertEq(
      spokeData.lastUpdateTimestamp,
      assetData.lastUpdateTimestamp,
      'spoke lastUpdateTimestamp post-withdraw'
    );
    // dai
    assertEq(tokenList.dai.balanceOf(address(spoke1)), 0, 'spoke token balance post-withdraw');
    assertEq(tokenList.dai.balanceOf(address(hub)), 0, 'hub token balance post-withdraw');
    assertEq(tokenList.dai.balanceOf(alice), MAX_SUPPLY_AMOUNT, 'user token balance post-withdraw');
  }

  // single asset, multiple spokes supplied. No drawn
  function test_withdraw_fuzz_multi_spoke(
    uint256 amount,
    uint256 amount2,
    uint32 riskPremium
  ) public {
    uint256 assetId = 0;
    amount = bound(amount, 1, MAX_SUPPLY_AMOUNT - 1);
    amount2 = bound(amount2, 1, MAX_SUPPLY_AMOUNT - amount);
    riskPremium %= MAX_RISK_PREMIUM_BPS; // no effect on withdraw because no drawn

    IERC20 asset = hub.assetsList(assetId);

    Utils.supply({
      hub: hub,
      assetId: assetId,
      spoke: address(spoke1),
      amount: amount,
      riskPremium: riskPremium,
      user: alice,
      to: address(spoke1)
    });
    Utils.supply({
      hub: hub,
      assetId: assetId,
      spoke: address(spoke2),
      amount: amount2,
      riskPremium: riskPremium,
      user: alice,
      to: address(spoke2)
    });

    Utils.withdraw({
      hub: hub,
      assetId: assetId,
      spoke: address(spoke1),
      amount: amount,
      riskPremium: 0,
      to: alice
    });
    Utils.withdraw({
      hub: hub,
      assetId: assetId,
      spoke: address(spoke2),
      amount: amount2,
      riskPremium: 0,
      to: alice
    });

    DataTypes.Asset memory assetData = hub.getAsset(assetId);
    DataTypes.SpokeData memory spokeData = hub.getSpoke(assetId, address(spoke1));
    DataTypes.SpokeData memory spoke2Data = hub.getSpoke(assetId, address(spoke2));

    // hub
    assertEq(hub.getTotalAssets(assetId), 0, 'hub total assets post-withdraw');
    // asset
    assertEq(assetData.suppliedShares, 0, 'asset total shares post-withdraw');
    assertEq(assetData.availableLiquidity, 0, 'asset availableLiquidity post-withdraw');
    assertEq(assetData.baseDebt, 0, 'asset baseDebt post-withdraw');
    assertEq(assetData.outstandingPremium, 0, 'asset outstandingPremium post-withdraw');
    assertEq(assetData.baseBorrowIndex, WadRayMath.RAY, 'asset baseBorrowIndex post-withdraw');
    assertEq(
      assetData.baseBorrowRate,
      uint256(5_00).bpsToRay(),
      'asset baseBorrowRate post-withdraw'
    );
    assertEq(assetData.riskPremium, 0, 'asset riskPremium post-withdraw');
    assertEq(
      assetData.lastUpdateTimestamp,
      vm.getBlockTimestamp(),
      'asset lastUpdateTimestamp post-withdraw'
    );
    // spoke
    assertEq(
      spokeData.suppliedShares,
      assetData.suppliedShares,
      'spoke suppliedShares post-withdraw'
    );
    assertEq(spokeData.baseDebt, assetData.baseDebt, 'spoke baseDebt post-withdraw');
    assertEq(
      spokeData.outstandingPremium,
      assetData.outstandingPremium,
      'spoke outstandingPremium post-withdraw'
    );
    assertEq(
      spokeData.baseBorrowIndex,
      assetData.baseBorrowIndex,
      'spoke baseBorrowIndex post-withdraw'
    );
    assertEq(spokeData.riskPremium, 0, 'spoke riskPremium post-withdraw');
    assertEq(
      spokeData.lastUpdateTimestamp,
      assetData.lastUpdateTimestamp,
      'spoke lastUpdateTimestamp post-withdraw'
    );
    // spoke
    assertEq(
      spoke2Data.suppliedShares,
      assetData.suppliedShares,
      'spoke suppliedShares post-withdraw'
    );
    assertEq(spoke2Data.baseDebt, assetData.baseDebt, 'spoke baseDebt post-withdraw');
    assertEq(
      spoke2Data.outstandingPremium,
      assetData.outstandingPremium,
      'spoke outstandingPremium post-withdraw'
    );
    assertEq(
      spoke2Data.baseBorrowIndex,
      assetData.baseBorrowIndex,
      'spoke baseBorrowIndex post-withdraw'
    );
    assertEq(spoke2Data.riskPremium, 0, 'spoke riskPremium post-withdraw');
    assertEq(
      spoke2Data.lastUpdateTimestamp,
      assetData.lastUpdateTimestamp,
      'spoke lastUpdateTimestamp post-withdraw'
    );
    // asset
    assertEq(asset.balanceOf(address(spoke1)), 0, 'spoke1 token balance post-withdraw');
    assertEq(asset.balanceOf(address(spoke2)), 0, 'spoke2 token balance post-withdraw');
    assertEq(asset.balanceOf(address(hub)), 0, 'hub token balance post-withdraw');
    assertEq(asset.balanceOf(alice), MAX_SUPPLY_AMOUNT, 'user token balance post-withdraw');
  }

  function test_withdraw_fuzz(uint256 assetId, uint256 amount) public {
    assetId = bound(assetId, 0, hub.assetCount() - 2); // Exclude duplicated DAI
    amount = bound(amount, 1, MAX_SUPPLY_AMOUNT);

    IERC20 asset = hub.assetsList(assetId);

    Utils.supply({
      hub: hub,
      assetId: assetId,
      spoke: address(spoke1),
      amount: amount,
      riskPremium: 0,
      user: alice,
      to: address(spoke1)
    });

    DataTypes.Asset memory assetData = hub.getAsset(assetId);
    DataTypes.SpokeData memory spokeData = hub.getSpoke(assetId, address(spoke1));

    uint256 timestamp = vm.getBlockTimestamp();

    // hub
    assertEq(hub.getTotalAssets(assetId), amount, 'hub total assets pre-withdraw');
    // asset
    assertEq(
      assetData.suppliedShares,
      hub.convertToShares(assetId, amount),
      'asset total shares pre-withdraw'
    );
    assertEq(assetData.availableLiquidity, amount, 'asset availableLiquidity pre-withdraw');
    assertEq(assetData.baseDebt, 0, 'asset baseDebt pre-withdraw');
    assertEq(assetData.outstandingPremium, 0, 'asset outstandingPremium pre-withdraw');
    assertEq(assetData.baseBorrowIndex, WadRayMath.RAY, 'asset baseBorrowIndex pre-withdraw');
    assertEq(
      assetData.baseBorrowRate,
      uint256(5_00).bpsToRay(),
      'asset baseBorrowRate pre-withdraw'
    );
    assertEq(assetData.riskPremium, 0, 'asset riskPremium pre-withdraw');
    assertEq(assetData.lastUpdateTimestamp, timestamp, 'asset lastUpdateTimestamp pre-withdraw');
    // spoke
    assertEq(
      spokeData.suppliedShares,
      assetData.suppliedShares,
      'spoke suppliedShares pre-withdraw'
    );
    assertEq(spokeData.baseDebt, assetData.baseDebt, 'spoke baseDebt pre-withdraw');
    assertEq(
      spokeData.outstandingPremium,
      assetData.outstandingPremium,
      'spoke outstandingPremium pre-withdraw'
    );
    assertEq(
      spokeData.baseBorrowIndex,
      assetData.baseBorrowIndex,
      'spoke baseBorrowIndex pre-withdraw'
    );
    assertEq(spokeData.riskPremium, 0, 'spoke riskPremium pre-withdraw');
    assertEq(
      spokeData.lastUpdateTimestamp,
      assetData.lastUpdateTimestamp,
      'spoke lastUpdateTimestamp pre-withdraw'
    );
    // asset
    assertEq(asset.balanceOf(address(spoke1)), 0, 'spoke token balance pre-withdraw');
    assertEq(asset.balanceOf(address(hub)), amount, 'hub token balance pre-withdraw');
    assertEq(
      asset.balanceOf(alice),
      MAX_SUPPLY_AMOUNT - amount,
      'alice token balance pre-withdraw'
    );

    vm.expectEmit(address(asset));
    emit IERC20.Transfer(address(hub), alice, amount);

    vm.expectEmit(address(hub));
    emit ILiquidityHub.Withdraw(assetId, address(spoke1), alice, amount);

    Utils.withdraw({
      hub: hub,
      assetId: assetId,
      spoke: address(spoke1),
      amount: amount,
      riskPremium: 0,
      to: alice
    });

    assetData = hub.getAsset(assetId);
    spokeData = hub.getSpoke(assetId, address(spoke1));

    // hub
    assertEq(hub.getTotalAssets(assetId), 0, 'hub total assets post-withdraw');
    // asset
    assertEq(assetData.suppliedShares, 0, 'asset total shares post-withdraw');
    assertEq(assetData.availableLiquidity, 0, 'asset availableLiquidity post-withdraw');
    assertEq(assetData.baseDebt, 0, 'asset baseDebt post-withdraw');
    assertEq(assetData.outstandingPremium, 0, 'asset outstandingPremium post-withdraw');
    assertEq(assetData.baseBorrowIndex, WadRayMath.RAY, 'asset baseBorrowIndex post-withdraw');
    assertEq(
      assetData.baseBorrowRate,
      uint256(5_00).bpsToRay(),
      'asset baseBorrowRate post-withdraw'
    );
    assertEq(assetData.riskPremium, 0, 'asset riskPremium post-withdraw');
    assertEq(assetData.lastUpdateTimestamp, timestamp, 'asset lastUpdateTimestamp post-withdraw');
    // spoke
    assertEq(
      spokeData.suppliedShares,
      assetData.suppliedShares,
      'spoke suppliedShares post-withdraw'
    );
    assertEq(spokeData.baseDebt, assetData.baseDebt, 'spoke baseDebt post-withdraw');
    assertEq(
      spokeData.outstandingPremium,
      assetData.outstandingPremium,
      'spoke outstandingPremium post-withdraw'
    );
    assertEq(
      spokeData.baseBorrowIndex,
      assetData.baseBorrowIndex,
      'spoke baseBorrowIndex post-withdraw'
    );
    assertEq(spokeData.riskPremium, 0, 'spoke riskPremium post-withdraw');
    assertEq(
      spokeData.lastUpdateTimestamp,
      assetData.lastUpdateTimestamp,
      'spoke lastUpdateTimestamp post-withdraw'
    );
    // asset
    assertEq(asset.balanceOf(address(spoke1)), 0, 'spoke token balance post-withdraw');
    assertEq(asset.balanceOf(address(hub)), 0, 'hub token balance post-withdraw');
    assertEq(asset.balanceOf(alice), MAX_SUPPLY_AMOUNT, 'alice token balance post-withdraw');
  }

  function test_withdraw_all_with_interest() public {
    uint256 daiAmount = 100e18;
    uint256 wethAmount = 10e18;
    uint256 drawAmount = daiAmount / 2;
    uint32 riskPremium = 20_00;
    uint256 lastUpdateTimestamp = vm.getBlockTimestamp();
    uint256 rate = uint256(10_00).bpsToRay();

    _supplyAndDrawLiquidity({
      daiAmount: daiAmount,
      wethAmount: wethAmount,
      daiDrawAmount: drawAmount,
      riskPremium: riskPremium,
      rate: rate
    });

    skip(365 days);

    HubData memory hubData;
    hubData.daiData = hub.getAsset(daiAssetId);

    uint256 initialAvailableLiquidity = hubData.daiData.availableLiquidity;
    uint256 supply2Amount = 10e18;

    // bob supplies more DAI to trigger accrual
    Utils.supply({
      hub: hub,
      assetId: daiAssetId,
      spoke: address(spoke2),
      amount: supply2Amount,
      riskPremium: 0,
      user: bob,
      to: address(spoke2)
    });

    hubData.daiData1 = hub.getAsset(daiAssetId);

    uint256 restoreAmount = hubData.daiData1.baseDebt + hubData.daiData1.outstandingPremium;
    uint256 newBaseBorrowIndex = WadRayMath.RAY +
      WadRayMath.RAY.rayMul(
        MathUtils.calculateLinearInterest(
          hubData.daiData1.baseBorrowRate,
          uint40(lastUpdateTimestamp)
        ) - WadRayMath.RAY
      );

    // alice restores all debt including accrual
    vm.prank(address(spoke1));
    hub.restore({assetId: daiAssetId, amount: restoreAmount, riskPremium: 0, repayer: alice});

    hubData.daiData2 = hub.getAsset(daiAssetId);
    assertEq(
      hubData.daiData2.availableLiquidity,
      initialAvailableLiquidity + restoreAmount + supply2Amount,
      'dai availableLiquidity'
    );

    // bob withdraws all liquidity with interest
    vm.prank(address(spoke2));
    hub.withdraw({
      assetId: daiAssetId,
      amount: hubData.daiData2.availableLiquidity,
      riskPremium: 0,
      to: bob
    });

    assertEq(
      tokenList.dai.balanceOf(bob),
      MAX_SUPPLY_AMOUNT + hubData.daiData2.availableLiquidity - supply2Amount - daiAmount,
      'bob dai balance'
    );

    hubData.daiData3 = hub.getAsset(daiAssetId);
    hubData.spoke1DaiData = hub.getSpoke(daiAssetId, address(spoke1));
    hubData.spoke2DaiData = hub.getSpoke(daiAssetId, address(spoke2));

    // hub
    assertEq(hub.getTotalAssets(daiAssetId), 0, 'hub totalAssets');
    assertEq(hubData.daiData3.suppliedShares, 0, 'dai suppliedShares');
    assertEq(hubData.daiData3.availableLiquidity, 0, 'dai availableLiquidity');
    assertEq(hubData.daiData3.baseDebt, 0, 'dai baseDebt');
    assertEq(hubData.daiData3.outstandingPremium, 0, 'dai outstandingPremium');
    assertEq(hubData.daiData3.baseBorrowIndex, newBaseBorrowIndex, 'dai baseBorrowIndex');
    assertEq(hubData.daiData3.baseBorrowRate, rate, 'dai baseBorrowRate');
    assertEq(hubData.daiData3.riskPremium, 0, 'dai riskPremium');
    assertEq(
      hubData.daiData3.lastUpdateTimestamp,
      vm.getBlockTimestamp(),
      'dai lastUpdateTimestamp'
    );
    // spoke1
    assertEq(hubData.spoke1DaiData.suppliedShares, 0, 'spoke1 suppliedShares');
    assertEq(hubData.spoke1DaiData.baseDebt, 0, 'spoke1 baseDebt');
    assertEq(hubData.spoke1DaiData.outstandingPremium, 0, 'spoke1 outstandingPremium');
    assertEq(hubData.spoke1DaiData.baseBorrowIndex, newBaseBorrowIndex, 'spoke1 baseBorrowIndex');
    assertEq(hubData.spoke1DaiData.riskPremium, 0, 'spoke1 riskPremium');
    assertEq(
      hubData.spoke1DaiData.lastUpdateTimestamp,
      vm.getBlockTimestamp(),
      'spoke1 lastUpdateTimestamp'
    );
    // spoke2
    assertEq(hubData.spoke2DaiData.suppliedShares, 0, 'spoke2 suppliedShares');
    assertEq(hubData.spoke2DaiData.baseDebt, 0, 'spoke2 baseDebt');
    assertEq(hubData.spoke2DaiData.outstandingPremium, 0, 'spoke2 outstandingPremium');
    assertEq(hubData.spoke2DaiData.baseBorrowIndex, newBaseBorrowIndex, 'spoke2 baseBorrowIndex');
    assertEq(hubData.spoke2DaiData.riskPremium, 0, 'spoke2 riskPremium');
    assertEq(
      hubData.spoke2DaiData.lastUpdateTimestamp,
      vm.getBlockTimestamp(),
      'spoke2 lastUpdateTimestamp'
    );
    // dai - all to alice
    assertEq(tokenList.dai.balanceOf(address(spoke1)), 0, 'spoke1 dai balance');
    assertEq(tokenList.dai.balanceOf(address(spoke2)), 0, 'spoke2 dai balance');
    assertEq(
      tokenList.dai.balanceOf(alice),
      MAX_SUPPLY_AMOUNT + drawAmount - restoreAmount,
      'alice dai balance'
    );
  }

  function test_withdraw_fuzz_all_liquidity_with_interest(
    uint256 drawAmount,
    uint32 riskPremium,
    uint256 rate,
    uint256 skipTime
  ) public {
    uint256 daiAmount = 100e18;
    uint256 wethAmount = 10e18;

    drawAmount = bound(drawAmount, 1, daiAmount); // within supplied dai amount
    skipTime = bound(skipTime, 1, 365 * 10 * 1 days); // 1 day to 10 years
    rate = bound(rate, 0, 200_00).bpsToRay(); // .1% to 200%
    riskPremium %= MAX_RISK_PREMIUM_BPS;

    uint256 lastUpdateTimestamp = vm.getBlockTimestamp();

    _supplyAndDrawLiquidity({
      daiAmount: daiAmount,
      wethAmount: wethAmount,
      daiDrawAmount: drawAmount,
      riskPremium: riskPremium,
      rate: rate
    });

    skip(skipTime);
    HubData memory hubData;
    hubData.daiData = hub.getAsset(daiAssetId);

    hubData.accruedBase = hubData.daiData.baseDebt.rayMul(rate);
    hubData.initialAvailableLiquidity = hubData.daiData.availableLiquidity;
    hubData.initialSupplyShares = hubData.daiData.suppliedShares;

    hubData.supply2Amount = 10e18;
    hubData.expectedSupply2Shares = hubData.supply2Amount.toSharesDown(
      hub.getTotalAssets(daiAssetId) + hubData.accruedBase,
      hubData.daiData.suppliedShares
    );

    // bob supplies more DAI to trigger accrual
    Utils.supply({
      hub: hub,
      assetId: daiAssetId,
      spoke: address(spoke2),
      amount: hubData.supply2Amount,
      riskPremium: 0,
      user: bob,
      to: address(spoke2)
    });

    hubData.daiData = hub.getAsset(daiAssetId);

    uint256 restoreAmount = hubData.daiData.baseDebt + hubData.daiData.outstandingPremium;
    uint256 newBaseBorrowIndex = WadRayMath.RAY +
      WadRayMath.RAY.rayMul(
        MathUtils.calculateLinearInterest(
          hubData.daiData.baseBorrowRate,
          uint40(lastUpdateTimestamp)
        ) - WadRayMath.RAY
      );

    // alice restores all debt including accrual
    vm.prank(address(spoke1));
    hub.restore({assetId: daiAssetId, amount: restoreAmount, riskPremium: 0, repayer: alice});

    hubData.daiData = hub.getAsset(daiAssetId);
    assertEq(
      hubData.daiData.availableLiquidity,
      hubData.initialAvailableLiquidity + restoreAmount + hubData.supply2Amount,
      'dai availableLiquidity'
    );

    // bob withdraws all liquidity with interest
    vm.prank(address(spoke2));
    hub.withdraw({
      assetId: daiAssetId,
      amount: hubData.daiData.availableLiquidity,
      riskPremium: 0,
      to: bob
    });

    assertEq(
      tokenList.dai.balanceOf(bob),
      MAX_SUPPLY_AMOUNT + hubData.daiData.availableLiquidity - hubData.supply2Amount - daiAmount,
      'bob dai balance'
    );

    hubData.daiData = hub.getAsset(daiAssetId);
    hubData.spoke1DaiData = hub.getSpoke(daiAssetId, address(spoke1));
    hubData.spoke2DaiData = hub.getSpoke(daiAssetId, address(spoke2));

    // hub
    assertEq(hub.getTotalAssets(daiAssetId), 0, 'hub totalAssets');
    assertEq(hubData.daiData.suppliedShares, 0, 'dai suppliedShares');
    assertEq(hubData.daiData.availableLiquidity, 0, 'dai availableLiquidity');
    assertEq(hubData.daiData.baseDebt, 0, 'dai baseDebt');
    assertEq(hubData.daiData.outstandingPremium, 0, 'dai outstandingPremium');
    assertEq(hubData.daiData.baseBorrowIndex, newBaseBorrowIndex, 'dai baseBorrowIndex');
    assertEq(hubData.daiData.baseBorrowRate, rate, 'dai baseBorrowRate');
    assertEq(hubData.daiData.riskPremium, 0, 'dai riskPremium');
    assertEq(
      hubData.daiData.lastUpdateTimestamp,
      vm.getBlockTimestamp(),
      'dai lastUpdateTimestamp'
    );
    // spoke1
    assertEq(hubData.spoke1DaiData.suppliedShares, 0, 'spoke1 suppliedShares');
    assertEq(hubData.spoke1DaiData.baseDebt, 0, 'spoke1 baseDebt');
    assertEq(hubData.spoke1DaiData.outstandingPremium, 0, 'spoke1 outstandingPremium');
    assertEq(hubData.spoke1DaiData.baseBorrowIndex, newBaseBorrowIndex, 'spoke1 baseBorrowIndex');
    assertEq(hubData.spoke1DaiData.riskPremium, 0, 'spoke1 riskPremium');
    assertEq(
      hubData.spoke1DaiData.lastUpdateTimestamp,
      vm.getBlockTimestamp(),
      'spoke1 lastUpdateTimestamp'
    );
    // spoke2
    assertEq(hubData.spoke2DaiData.suppliedShares, 0, 'spoke2 suppliedShares');
    assertEq(hubData.spoke2DaiData.baseDebt, 0, 'spoke2 baseDebt');
    assertEq(hubData.spoke2DaiData.outstandingPremium, 0, 'spoke2 outstandingPremium');
    assertEq(hubData.spoke2DaiData.baseBorrowIndex, newBaseBorrowIndex, 'spoke2 baseBorrowIndex');
    assertEq(hubData.spoke2DaiData.riskPremium, 0, 'spoke2 riskPremium');
    assertEq(
      hubData.spoke2DaiData.lastUpdateTimestamp,
      vm.getBlockTimestamp(),
      'spoke2 lastUpdateTimestamp'
    );
    // dai - all to alice
    assertEq(tokenList.dai.balanceOf(address(spoke1)), 0, 'spoke1 dai balance');
    assertEq(tokenList.dai.balanceOf(address(spoke2)), 0, 'spoke2 dai balance');
    assertEq(
      tokenList.dai.balanceOf(alice),
      MAX_SUPPLY_AMOUNT + drawAmount - restoreAmount,
      'alice dai balance'
    );
  }

  function test_withdraw_revertsWith_SuppliedAmountExceeded_zero_supplied() public {
    uint256 assetId = 0;
    uint256 amount = 1;

    vm.expectRevert(abi.encodeWithSelector(ILiquidityHub.SuppliedAmountExceeded.selector, 0));
    vm.prank(address(spoke1));
    hub.withdraw({assetId: assetId, amount: amount, riskPremium: 0, to: address(spoke1)});
  }

  function test_withdraw_revertsWith_SuppliedAmountExceeded() public {
    uint256 assetId = daiAssetId;
    uint256 amount = 100e18;

    // User supply
    Utils.supply({
      hub: hub,
      assetId: assetId,
      spoke: address(spoke1),
      amount: amount,
      riskPremium: 0,
      user: alice,
      to: address(spoke1)
    });

    vm.expectRevert(abi.encodeWithSelector(ILiquidityHub.SuppliedAmountExceeded.selector, amount));
    vm.prank(address(spoke1));
    hub.withdraw({assetId: assetId, amount: amount + 1, riskPremium: 0, to: alice});

    // advance time, but no accumulation
    skip(1e18);

    vm.expectRevert(abi.encodeWithSelector(ILiquidityHub.SuppliedAmountExceeded.selector, amount));
    vm.prank(address(spoke1));
    hub.withdraw({assetId: assetId, amount: amount + 1, riskPremium: 0, to: alice});
  }

  function test_withdraw_revertsWith_NotAvailableLiquidity() public {
    uint256 amount = 100e18;

    // User supply
    Utils.supply({
      hub: hub,
      assetId: daiAssetId,
      spoke: address(spoke1),
      amount: amount,
      riskPremium: 0,
      user: alice,
      to: address(spoke1)
    });

    // spoke1 draw all of dai reserve liquidity
    Utils.draw({
      hub: hub,
      assetId: daiAssetId,
      spoke: address(spoke1),
      amount: amount,
      to: alice,
      riskPremium: 0,
      onBehalfOf: address(spoke1)
    });

    vm.expectRevert(abi.encodeWithSelector(ILiquidityHub.NotAvailableLiquidity.selector, 0));
    vm.prank(address(spoke1));
    hub.withdraw({assetId: daiAssetId, amount: amount, riskPremium: 0, to: address(spoke1)});
  }

  function test_withdraw_revertsWith_InvalidWithdrawAmount() public {
    uint256 amount = 100e18;

    // User supply
    Utils.supply({
      hub: hub,
      assetId: daiAssetId,
      spoke: address(spoke1),
      amount: amount,
      riskPremium: 0,
      user: alice,
      to: address(spoke1)
    });

    vm.expectRevert(ILiquidityHub.InvalidWithdrawAmount.selector);
    vm.prank(address(spoke1));
    hub.withdraw({assetId: daiAssetId, amount: 0, riskPremium: 0, to: alice});
  }

  function test_withdraw_revertsWith_AssetNotActive() public {
    uint256 amount = 100e18;
    updateAssetActive(hub, daiAssetId, false);

    assertFalse(hub.getAssetConfig(daiAssetId).active);

    vm.expectRevert(ILiquidityHub.AssetNotActive.selector);
    vm.prank(address(spoke1));
    hub.withdraw({assetId: daiAssetId, amount: amount, riskPremium: 0, to: alice});
  }

  function test_withdraw_revertsWith_AssetPaused() public {
    uint256 amount = 100e18;
    updateAssetPaused(hub, daiAssetId, true);

    assertTrue(hub.getAssetConfig(daiAssetId).paused);

    vm.expectRevert(ILiquidityHub.AssetPaused.selector);
    vm.prank(address(spoke1));
    hub.withdraw({assetId: daiAssetId, amount: amount, riskPremium: 0, to: alice});
  }
}
