// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.0;

/**
 * @title WadRayMathExtended library, with explicit rounding
 * @author Aave
 * @notice Provides functions to perform calculations with Wad and Ray units
 * @dev Provides mul and div function for wads (decimal numbers with 18 digits of precision) and rays (decimal numbers
 * with 27 digits of precision)
 */

library WadRayMathExtended {
  uint256 internal constant WAD = 1e18;
  uint256 internal constant RAY = 1e27;

  /**
   * @dev Multiplies two wad, rounding down
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Wad
   * @param b Wad
   * @return c = floor(a*b), in wad
   */
  function wadMulDown(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= type(uint256).max / b
    assembly ('memory-safe') {
      if iszero(or(iszero(b), iszero(gt(a, div(not(0), b))))) {
        revert(0, 0)
      }

      c := div(mul(a, b), WAD)
    }
  }

  /**
   * @dev Multiplies two wad, rounding up
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Wad
   * @param b Wad
   * @return c = ceil(a*b), in wad
   */
  function wadMulUp(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= type(uint256).max / b
    assembly ('memory-safe') {
      if iszero(or(iszero(b), iszero(gt(a, div(not(0), b))))) {
        revert(0, 0)
      }
      c := mul(a, b)
      // Add 1 if (a * b) % WAD > 0 to round up the division of (a * b) by WAD
      c := add(div(c, WAD), gt(mod(c, WAD), 0))
    }
  }

  /**
   * @dev Divides two wad, rounding down
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Wad
   * @param b Wad
   * @return c = floor(a/b), in wad
   */
  function wadDivDown(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= type(uint256).max / WAD
    assembly ('memory-safe') {
      if or(iszero(b), iszero(iszero(gt(a, div(not(0), WAD))))) {
        revert(0, 0)
      }

      c := div(mul(a, WAD), b)
    }
  }

  /**
   * @dev Divides two wad, rounding up
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Wad
   * @param b Wad
   * @return c = ceil(a/b), in wad
   */
  function wadDivUp(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= type(uint256).max / WAD
    assembly ('memory-safe') {
      if or(iszero(b), iszero(iszero(gt(a, div(not(0), WAD))))) {
        revert(0, 0)
      }
      c := mul(a, WAD)
      // Add 1 if (a * WAD) % b > 0 to round up the division of (a * WAD) by b
      c := add(div(c, b), gt(mod(c, b), 0))
    }
  }

  /**
   * @dev Multiplies two ray, rounding down
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Ray
   * @param b Ray
   * @return c = floor(a*b), in ray
   */
  function rayMulDown(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= type(uint256).max / b
    assembly ('memory-safe') {
      if iszero(or(iszero(b), iszero(gt(a, div(not(0), b))))) {
        revert(0, 0)
      }

      c := div(mul(a, b), RAY)
    }
  }

  /**
   * @dev Multiplies two ray, rounding up
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Ray
   * @param b Ray
   * @return c = ceil(a*b), in ray
   */
  function rayMulUp(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= type(uint256).max / b
    assembly ('memory-safe') {
      if iszero(or(iszero(b), iszero(gt(a, div(not(0), b))))) {
        revert(0, 0)
      }
      c := mul(a, b)
      // Add 1 if (a * b) % RAY > 0 to round up the division of (a * b) by RAY
      c := add(div(c, RAY), gt(mod(c, RAY), 0))
    }
  }

  /**
   * @dev Divides two ray, rounding down
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Ray
   * @param b Ray
   * @return c = floor(a/b), in ray
   */
  function rayDivDown(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= type(uint256).max / RAY
    assembly ('memory-safe') {
      if or(iszero(b), iszero(iszero(gt(a, div(not(0), RAY))))) {
        revert(0, 0)
      }

      c := div(mul(a, RAY), b)
    }
  }

  /**
   * @dev Divides two ray, rounding up
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Ray
   * @param b Ray
   * @return c = ceil(a/b), in ray
   */
  function rayDivUp(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= type(uint256).max / RAY
    assembly ('memory-safe') {
      if or(iszero(b), iszero(iszero(gt(a, div(not(0), RAY))))) {
        revert(0, 0)
      }
      c := mul(a, RAY)
      // Add 1 if (a * RAY) % b > 0 to round up the division of (a * RAY) by b
      c := add(div(c, b), gt(mod(c, b), 0))
    }
  }
}
